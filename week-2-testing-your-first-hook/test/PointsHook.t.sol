// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
 
import {Deployers} from "@uniswap/v4-core/test/utils/Deployers.sol";
import {PoolSwapTest} from "v4-core/test/PoolSwapTest.sol";
import {MockERC20} from "solmate/src/test/utils/mocks/MockERC20.sol";
 
import {SwapParams, ModifyLiquidityParams} from "v4-core/types/PoolOperation.sol";
 
import {Currency} from "v4-core/types/Currency.sol";
import {PoolId} from "v4-core/types/PoolId.sol";
 
import {Hooks} from "v4-core/libraries/Hooks.sol";
import {TickMath} from "v4-core/libraries/TickMath.sol";
import {LiquidityAmounts} from "@uniswap/v4-core/test/utils/LiquidityAmounts.sol";
 
import {ERC1155TokenReceiver} from "solmate/src/tokens/ERC1155.sol";
 
import {PointsHook} from "../src/PointsHook.sol";
 
contract TestPointsHook is Test, Deployers, ERC1155TokenReceiver {
 
	MockERC20 token; // our token to use in the ETH-TOKEN pool
 
	// Native tokens are represented by address(0)
	Currency ethCurrency = Currency.wrap(address(0));
	Currency tokenCurrency;
 
	PointsHook hook;

    function setUp() public {
        // Step 1 + 2
        // Deploy PoolManager and Router contracts
        deployFreshManagerAndRouters();

        // Deploy our TOKEN contract
        token = new MockERC20("Test Token", "TEST", 18);
        tokenCurrency = Currency.wrap(address(token));
    
        // Mint a bunch of TOKEN to ourselves and to address(1)
        token.mint(address(this), type(uint128).max);
        token.mint(address(1), type(uint128).max);
    
        // Deploy hook to an address that has the proper flags set
        uint160 flags = uint160(Hooks.AFTER_SWAP_FLAG);
        deployCodeTo("PointsHook.sol", abi.encode(manager), address(flags));

        // Deploy our hook
        hook = PointsHook(address(flags));
    
        // Approve our TOKEN for spending on the swap router and modify liquidity router
        // These variables are coming from the `Deployers` contract
        token.approve(address(swapRouter), type(uint256).max);
        token.approve(address(modifyLiquidityRouter), type(uint256).max);
    
        // Initialize a pool
        (key, ) = initPool(
            ethCurrency, // Currency 0 = ETH
            tokenCurrency, // Currency 1 = TOKEN
            hook, // Hook Contract
            3000, // Swap Fees
            SQRT_PRICE_1_1 // Initial Sqrt(P) value = 1
        );
    
        // Add some liquidity to the pool
        uint160 sqrtPriceAtTickLower = TickMath.getSqrtPriceAtTick(-60);
        uint160 sqrtPriceAtTickUpper = TickMath.getSqrtPriceAtTick(60);
    
        uint256 ethToAdd = 0.003 ether;
        uint128 liquidityDelta = LiquidityAmounts.getLiquidityForAmount0(
            SQRT_PRICE_1_1,
            sqrtPriceAtTickUpper,
            ethToAdd
        );
        uint256 tokenToAdd = LiquidityAmounts.getAmount1ForLiquidity(
            sqrtPriceAtTickLower,
            SQRT_PRICE_1_1,
            liquidityDelta
        );
    
        modifyLiquidityRouter.modifyLiquidity{value: ethToAdd}(
            key,
            ModifyLiquidityParams({
                tickLower: -60,
                tickUpper: 60,
                liquidityDelta: int256(uint256(liquidityDelta)),
                salt: bytes32(0)
            }),
            ZERO_BYTES
        );
    }

    // Basic unit test for the swap function
    // - We need to make sure that we can spend ETH and receive points
    // - Spend 0.001 ETH
    // - Receive 20% of 0.001 = 0.0002 points
    function test_swap_success() public {
        // Get the PoolId uint
        uint256 poolIdUint = uint256(PoolId.unwrap(key.toId()));

        // Using this PoolIdUint value we can find the start balance of the user points
        uint256 pointsBalanceOriginal = hook.balanceOf(
            address(this),
            poolIdUint
        );
    
        // How can we confirm that we have the right value?
        assertEq(pointsBalanceOriginal, 0, 'Points balance is not 0');

        // Set user address in hook data
        bytes memory hookData = abi.encode(address(this));
    
        // Now we swap
        // We will swap 0.001 ether for tokens
        // We should get 20% of 0.001 * 10**18 points
        // = 2 * 10**14
        swapRouter.swap{value: 0.001 ether}(
            key,
            SwapParams({
                zeroForOne: true,
                amountSpecified: -0.001 ether, // Exact input for output swap
                sqrtPriceLimitX96: TickMath.MIN_SQRT_PRICE + 1
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            hookData
        );

        uint256 pointsBalanceAfterSwap = hook.balanceOf(
            address(this),
            poolIdUint
        );

        assertEq(pointsBalanceAfterSwap - pointsBalanceOriginal, 0.0002 ether, 'Points balance is not correct');
    }

    // Swap TOKEN for TOKEN -> Get no points
    // Test overflow amounts of tokens

    // Swap TOKEN for ETH -> Get no points
    function test_swap_tokenForEth() public {
        // Get the PoolId uint
        uint256 poolIdUint = uint256(PoolId.unwrap(key.toId()));

        // Using this PoolIdUint value we can find the start balance of the user points
        uint256 pointsBalanceOriginal = hook.balanceOf(
            address(this),
            poolIdUint
        );
    
        // How can we confirm that we have the right value?
        assertEq(pointsBalanceOriginal, 0, 'Points balance is not 0');

        // Set user address in hook data
        bytes memory hookData = abi.encode(address(this));
    
        // Now we swap
        // We will swap 0.001 ether for tokens
        // We should get 20% of 0.001 * 10**18 points
        // = 2 * 10**14
        swapRouter.swap(
            key,
            SwapParams({
                zeroForOne: false,
                amountSpecified: -0.001 ether, // Exact input for output swap
                sqrtPriceLimitX96: TickMath.MAX_SQRT_PRICE - 1
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            hookData
        );

        // Using this PoolIdUint value we can find the start balance of the user points
        uint256 pointsBalanceAfterSwap = hook.balanceOf(
            address(this),
            poolIdUint
        );
    
        // How can we confirm that we have the right value?
        assertEq(pointsBalanceOriginal, pointsBalanceAfterSwap, 'Points balance is not 0');
    }
    
    // Swap a number not divisible by 5 (e.g. < 5) -> Get no points
    function test_swap_swapDust() public {
        // ..
    }

    function testFuzz_swap(uint256 _amount, bool _zeroForOne, address _pointsRecipient) public {
        // To ensure that we can convert the uint to an int, we need to ensure the value does not go over uint128
        // vm.assume(_amount < type(uint128).max);
        _amount = bound(_amount, 5, type(uint128).max);

        // Ensure that we have enough tokens to fill the swap
        if (_zeroForOne) {
            // We need more ETH
            deal(address(this), _amount);
        } else {
            // We need more TOKEN
            deal(address(token), address(this), _amount);
            token.approve(address(swapRouter), _amount);
        }

        uint256 poolIdUint = uint256(PoolId.unwrap(key.toId()));
        uint256 pointsBalanceOriginal = hook.balanceOf(_pointsRecipient, poolIdUint);
    
        // Set user address in hook data
        bytes memory hookData = abi.encode(_pointsRecipient);
    
        // Now we swap
        // We will swap 0.001 ether for tokens
        // We should get 20% of 0.001 * 10**18 points
        // = 2 * 10**14
        swapRouter.swap{value: _zeroForOne ? _amount : 0}(
            key,
            SwapParams({
                zeroForOne: _zeroForOne,
                // forge-lint: disable-next-line(unsafe-typecast)
                amountSpecified: -int256(_amount), // Exact input for output swap
                sqrtPriceLimitX96: _zeroForOne ? TickMath.MIN_SQRT_PRICE + 1 : TickMath.MAX_SQRT_PRICE - 1
            }),
            PoolSwapTest.TestSettings({
                takeClaims: false,
                settleUsingBurn: false
            }),
            hookData
        );

        uint256 pointsBalanceAfterSwap = hook.balanceOf(_pointsRecipient, poolIdUint);

        // @todo We only gain points if the are swapping ETH for TOKEN
        if (_zeroForOne) {
            assertGt(pointsBalanceAfterSwap, pointsBalanceOriginal, 'No points gained');
        } else {
            assertEq(pointsBalanceAfterSwap, pointsBalanceOriginal, 'Points balance is not correct');
        }
    }

}